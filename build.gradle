plugins {
    id "fabric-loom" version "${loom_version}" apply false
}

ext {
    activeAdapter = findProperty("reactivemusic.adapter") ?: "1_21_1"
    
    minecraftTargets = [
        "1_21_1": [
            minecraftVersion: "1.21.1",
            yarnMappings: "1.21.1+build.3",
            loaderVersion: "0.16.14",
            fabricApiVersion: "0.114.0+1.21.1",
            modMenuVersion: "11.0.3",
            yaclVersion: "3.6.2+1.21-fabric"
        ],
        "1_20_4": [
            minecraftVersion: "1.20.4", 
            yarnMappings: "1.20.4+build.3",
            loaderVersion: "0.15.11",
            fabricApiVersion: "0.97.0+1.20.4",
            modMenuVersion: "11.0.3",
            yaclVersion: "3.6.2+1.21-fabric"
        ],
        "latest": [
            minecraftVersion: "1.21.6",
            yarnMappings: "1.21.6+build.1", 
            loaderVersion: "0.17.2",
            fabricApiVersion: "0.128.2+1.21.6",
            modMenuVersion: "15.0.0",
            yaclVersion: "3.8.0+1.21.6-fabric"
        ]
    ]
    
    activeConfig = minecraftTargets[activeAdapter]
}

allprojects {
    group = maven_group
}

def activeProjectName = "adapters-${activeAdapter}"
def activeProject = findProject(":${activeProjectName}")

if (activeProject != null) {
    configure(activeProject) {
        apply plugin: "fabric-loom"
        apply plugin: "java"
        
        repositories {
            mavenCentral()
            maven { url "https://maven.fabricmc.net/" }
            maven { url "https://maven.terraformersmc.com/releases/" }
            maven { url "https://maven.isxander.dev/releases" }
        }
        
        version = "${mod_version}+${activeConfig.minecraftVersion}"
        base.archivesName = "${archives_base_name}-${activeAdapter}"
        
        sourceSets {
            main {
                java.srcDirs = ["src/main/java", "../src/main/java"]
                resources.srcDirs = ["src/main/resources", "../src/main/resources"]
            }
        }
        
        dependencies {
            minecraft "com.mojang:minecraft:${activeConfig.minecraftVersion}"
            mappings "net.fabricmc:yarn:${activeConfig.yarnMappings}:v2"
            modImplementation "net.fabricmc:fabric-loader:${activeConfig.loaderVersion}"
            modImplementation "net.fabricmc.fabric-api:fabric-api:${activeConfig.fabricApiVersion}"
            modApi "com.terraformersmc:modmenu:${activeConfig.modMenuVersion}"
            modImplementation "dev.isxander:yet-another-config-lib:${activeConfig.yaclVersion}"
        }
        
        loom {
            mixin.defaultRefmapName = "reactivemusic.refmap.json"
        }
        
        java {
            toolchain.languageVersion = JavaLanguageVersion.of(21)
            withSourcesJar()
        }
        
        tasks.withType(JavaCompile) {
            options.release = 21
        }
    }
}
